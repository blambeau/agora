# about:
#  name: The Mine Pump
#  version: 0.1.0
#  authors:
#    - Axel van Lamsweerde <axel.vanlamsweerde@uclouvain.be>
#    - Bernard Lambeau <bernard.lambeau@uclouvain.be>
#  description: KAOS Analysis for The Mine Pump Case Study

declare agent
  name "HighWaterSensor"
  description "Legacy sensor for measuring water level"
  #kind "environment"
end

declare agent
  name "PumpActuator"
  description "Legacy actuator for pumps"
  #type "environment"
end

declare agent
  name "SafetyController"
  definition "Software to develop to control the pumps inside the mine"
  #type "software"
end

declare goal
  name "Avoid[Miners In FloodedMine]"
  definition "Miners may never be blocked in a flooded mine"
  refinedby "DomHyp[Sumps Well Distributed]", "Maintain[Sump Pumped Out If High Water]"
end

declare domhyp
  name "DomHyp[Sumps Well Distributed]"
  definition "By hypothesis, mine sumps are well distributed"
end

declare goal
  name "Maintain[Sump Pumped Out If High Water]"
  definition "When the water level is high, sumps are always pumped out so that miner are never blocked inside a pumped mine."
  refinedby "Maintain[PumpOn If High Water]",
            "DomHyp[Water Pumped Out If PumpOn]",
            "DomHyp[Sufficient Pump Capacity]",
            "DomHyp[No Excessive Water Flow]"
end

declare domhyp
  name "DomHyp[Water Pumped Out If PumpOn]"
  definition "By hypothesis, we consider that if a pump is on, it pumps water out"
end

declare domhyp
  name "DomHyp[Sufficient Pump Capacity]"
  definition "By hypothesis, we consider that the mine is sufficiently equiped with well chosen pump capacities"
end

declare domhyp
  name "DomHyp[No Excessive Water Flow]"
  definition "By hypothesis, we consider that the water flow is not excessive according to the number of pumps in the mine"
end

declare goal
  name "Maintain[PumpOn If High Water]"
  definition "When the water level is high, the pump must be on."
  refinedby "Maintain[High Water Detected]",
            "Maintain[PumpOn If High Water Detected]"
end

declare goal
  name "Maintain[High Water Detected]"
  definition "Each sump will be be equipped with water-level sensors to detect when the water is above a high or below a low level."
  assignedto "HighWaterSensor"
end

declare goal
  name "Maintain[PumpOn If High Water Detected]"
  definition "When the high water level is detected, the pump is on"
  refinedby "Maintain[Pump Switch On If High Water Detected]",
            "Maintain[PumpOn IIf Pump Switch On]"
end

declare goal
  name "Maintain[Pump Switch On If High Water Detected]"
  definition "When the high water level is detected, the pump switch is on"
  assignedto "SafetyController"
end

declare goal
  name "Maintain[PumpOn IIf Pump Switch On]"
  definition "Every time the pump switch is on, the pump is on as well"
  assignedto "PumpActuator"
end